'
'		Breakout. Paul Robson.
'
call Initialise()
call NewGame()
call RedrawFrame()
call ResetBat()
call ResetBall()
repeat
	if event(tBat,4) then call MoveBat()
	if event(tBall,2) then call MoveBall()
until lives = 0
end

'
'		Initialise 
'
proc Initialise()
	wallDepth = 6
	frameWidth = 8
	backWall = 32
	brickHeight = 8
	brickWidthCount = (320-frameWidth*2)\16
	dim brick(brickWidthCount,wallDepth),colour(wallDepth),yCentre(wallDepth)
	for i = 1 to 6
		yCentre(i) = 110-brickHeight*i
		read colour(i) 
	next
	dim score(3)
	data 13,2,3,12,11,1
	defchr 192,31,17,17,17,17,17,31
	defchr 193,1,1,1,1,1,1,1
	defchr 194,31,1,1,31,16,16,31
	defchr 195,31,1,1,31,1,1,31
	defchr 196,17,17,17,31,1,1,1
	defchr 197,31,16,16,31,1,1,31
	defchr 198,31,16,16,31,17,17,31
	defchr 199,31,1,1,1,1,1,1
	defchr 200,31,17,17,31,17,17,31
	defchr 201,31,17,17,31,1,1,31
endproc
'
'		New Game
'
proc NewGame()
	local i
	for i = 1 to 3:score(i) = 0:next
	lives = 5:batWidth = 32
endproc
'
'		Redraw frame
'
proc RedrawFrame()
	cls:sprite clear
	rect ink 15 solid from 0,26 to frameWidth,239
	rect ink 15 solid from 319,26 to 319-frameWidth,239
	rect ink 15 solid from 0,26 to 319,backWall
	for i = 1 to wallDepth
		rect ink colour(i) solid from 0,yCentre(i)-brickHeight\2 to 319,yCentre(i)+brickHeight\2-1
		for j = 0 to brickWidthCount-1:brick(j,i) = 1:next
	next
	brickRemoved = 0
	brickCount = wallDepth * brickWidthCount
	call RedrawScore():call RedrawLives()
endproc
'
'		Redraw the score
'
proc RedrawScore()
	local i
	for i = 1 to 3:call DrawDigit(i+2,score(i)):next
endproc
'
'		Redraw the lives count
'
proc RedrawLives()
	call DrawDigit(14,lives)
endproc
'
'		Draw a digit
'
proc DrawDigit(x,d)
	x = x * 18
	text chr$(d+192) ink 15 dim 3 to x,1
endproc
'
'		Reset the bat
'
proc ResetBat()
	xBat = 160:yBat = 230:widthBat = 32
	sprite 1 image $C0 anchor 8 to xBat,yBat
endproc
'
'		Move the bat
'
proc MoveBat()
	local dx,dy
	dy = joypad(dx,dy)
	xBat = min(319-frameWidth-batWidth\2,max(frameWidth+batWidth\2,xBat + dx * 8))
	sprite 1 to xBat,yBat
endproc
'
'		Reset the ball
'
proc ResetBall()
	xBall = rand(160)+80:yBall = 200
	xiBall = 4:yiBall = 4
	sprite 2 image $80 to xBall,yBall
endproc
'
'		Move the ball
'
proc MoveBall()
	xBall = xBall + xiBall:yBall = yBall + yiBall
	if xBall < frameWidth+3 | xBall >= 317-frameWidth then xiBall = -xiBall
	if yBall < backWall+3 then yiBall = -yiBall
	if yBall > 230 then yiBall = -yiBall:xiBall = rand(9)-4
	if yBall <= yCentre(1) & yBall >= yCentre(wallDepth)-brickHeight\2
		call CheckBrick()
	endif
	sprite 2 to xBall,yBall
endproc
'
'		Check hit brick
'
proc CheckBrick()
	local x,y
	x = (xBall - frameWidth) \ 16
	y = (yCentre(1)-yBall) \ brickHeight + 1
	if brick(x,y) <> 0
		call RemoveBrick(x,y):yiBall = -yiBall
	endif
endproc
'
'		Remove brick
'
proc RemoveBrick(x,y)
	brick(x,y) = 0
	x = x * 16 + frameWidth:y = yCentre(y)-brickHeight\2
	rect solid ink 0 from x,y to x+15,y+brickHeight

