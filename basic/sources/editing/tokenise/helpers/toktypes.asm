; ************************************************************************************************
; ************************************************************************************************
;
;		Name:		toktypes.asm
;		Purpose:	Check various types
;		Created:	14th December 2023
;		Reviewed:   No
;		Author:		Paul Robson (paul@robsons.org.uk)
;
; ************************************************************************************************
; ************************************************************************************************
		
		.section code

; ************************************************************************************************
;
;									Convert to Upper/Lower Case
;
; ************************************************************************************************

TOKToUpper:
		cmp 	#"a"
		bcc 	_TTUNotLower
		cmp 	#"z"+1
		bcs 	_TTUNotLower
		eor 	#$20
_TTUNotLower:		
		rts

TOKToLower:
		cmp 	#"A"
		bcc 	_TTUNotUpper
		cmp 	#"Z"+1
		bcs 	_TTUNotUpper
		eor 	#$20
_TTUNotUpper:		
		rts

; ************************************************************************************************
;
;										Check is digit
;
; ************************************************************************************************

TOKIsDigit:
		cmp 	#"0"
		bcc 	TOKFail
		cmp 	#"9"+1
		bcs 	TOKFail
TOKSucceed:
		sec
		rts
TOKFail:	
		clc
		rts

; ************************************************************************************************
;
;								Part of identifier (a-z0-9_)
;
; ************************************************************************************************

TOKIsIdentifierElement:
		cmp 	#"_"
		beq 	TOKSucceed 					; else fall through to alphanumeric
		
; ************************************************************************************************
;
;										Check is alphanumeric
;
; ************************************************************************************************

TOKIsAlphaNumeric:
		jsr 	TOKIsAlpha
		bcs 	TOKSucceed
		jsr 	TOKIsDigit
		rts
		
; ************************************************************************************************
;
;										Check is alpha
;
; ************************************************************************************************

TOKIsAlpha:
		jsr 	TOKToUpper
		cmp 	#"A"
		bcc 	TOKFail
		cmp 	#"Z"+1
		bcs 	TOKFail
		bra 	TOKSucceed

; ************************************************************************************************
;
;									Check is hexadecimal
;
; ************************************************************************************************

TOKIsHexadecimal:
		jsr		TOKIsDigit
		bcs 	TOKSucceed
		jsr 	TOKIsAlpha
		bcc 	TOKFail
		cmp 	#"F"+1
		bcs 	TOKFail
		bra 	TOKSucceed
		
		.send code
		
; ************************************************************************************************
;
;									Changes and Updates
;
; ************************************************************************************************
;
;		Date			Notes
;		==== 			=====
;
; ************************************************************************************************

